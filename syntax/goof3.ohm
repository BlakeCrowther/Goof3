Goof3 {
    Program     = Block
    Block       = (Line | Advanced)+
    Line        = comment
                | Statement ":"                                      -- stmt
    Advanced    = Function | If | Loop
    Statement   = "goof" id "=="+ Exp                                -- assignment
                | "goof" id                                          -- declaration
                | "yeet" StringLit                                   -- throw
                | VarExp "=="+ Exp                                   -- reassignment
                | "poof" "(" StringLit ")"                           -- print
                | "return" Exp                                       -- return

    Function    = "phoof" id "(" ListOf<Exp, ","> ")" "}" Block "{"  -- declaration
    If          = "gif" "(" Exp ")" "}" Block "{"
                  ("elsegif" "(" Exp ")" "}" Block "{")*
                  ("else" "}" Block "{")*
    Loop        = "four" "(" ListOf<Statement_assignment, ","> 
                  ":" Exp ":" Exp ")" "}" Block "{"                  -- for
                | "wooloop" "(" Exp ")" "}" Block "{"                -- while
    
    Exp         = Exp logOp Exp1                                     -- logical
                | Exp1
    Exp1        = Exp1 relOp Exp1                                    -- relative
                | Exp2
    Exp2        = Exp2 addop Exp3                                    -- addition
                | Exp3
    Exp3        = Exp3 mulOp Exp4                                    -- multiplication
                | Exp4
    Exp4        = Exp5 sufixOp                                       -- increment
                | Exp5
    Exp5        = id "(" ListOf<Exp, ","> ")"                        -- fCall
                | VarExp
                | numlit
                | StringLit
    VarExp      = id
    comment     = "xD" (~newline any)* newline                       -- singleLine
                | ";)" (~"(;" any)* "(;"                             -- multiLine

    logOp       = "||" | "&&"
    relOp       = "<=" "=="*
                | "<" space
                | "=" "=="*
                | "!=" "=="*
                | ">=" "=="*
                | ">" space
    mulOp       = "*" | "/" | "%"
    sufixOp     = "++" | "--"
    addop       = "+" | "-"

    keyword     = ("goof" | "yeet" | "poof" | "return" | "phoof" |
                  "gif" | "four" | "wooloop" | "xD" | ";)" | "(;") ~idrest
    id          = ~keyword letter idrest*
    idrest      = "_" | alnum
    StringLit   = "\"" (~"\"" any)* "\""                            -- basic
                // | "`" (("${" Exp "}")? (~"`" any) 
                //   ("${" Exp "}")?)* "`"                             -- fancy
    intlit      = digit+
    declit      = digit+ "." digit*
    numlit      = declit | intlit
    newline     = "\n"
}